plugins {
    id 'com.jfrog.bintray' version '1.8.4'
}

ext {
    genSrcDir = 'src-gen/main/java'
}

sourceSets {
    main.java.srcDir genSrcDir
}

configurations {
    jaxb
}

dependencies {
    compile project(':impexp-core')
    compile project(':impexp-plugin-api')
    compile project(':impexp-client-common')
    compile 'org.citydb:texture-atlas-creator:1.4.0'
    compile 'java3d:j3d-core:1.5.2'
    compile 'java3d:j3d-core-utils:1.5.2'
    compile 'java3d:vecmath:1.5.2'

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.11.1'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:1.3'
    jaxb 'org.slf4j:slf4j-simple:1.7.25'
}

task generateJaxb(group: 'jaxb') {
    outputs.dir genSrcDir

    doLast {
        mkdir genSrcDir

        System.setProperty('javax.xml.accessExternalSchema', 'file')
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.xjc(destdir: genSrcDir,
                schema: 'resources/jaxb/kml-collada.xsd',
                binding: 'resources/jaxb/binding.xjb',
                extension: 'true',
                encoding: 'UTF-8') {
            produces(dir: genSrcDir, includes: '**/*.java')
            arg(line: '-Xnamespace-prefix -Xsetters -Xsetters-mode=direct')
        }

        delete "$genSrcDir/oasis"
    }
}

task cleanJaxb(type: Delete, group: 'jaxb') {
    delete 'src-gen'
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = '3dcitydb'
        name = project.name
        websiteUrl = project.citydbVcsUrl
        vcsUrl = project.impexpGit
        issueTrackerUrl = project.impexpIssueTrackerUrl
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}