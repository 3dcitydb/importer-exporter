apply plugin: 'application'

applicationName = '3DCityDB-Importer-Exporter'
mainClassName = 'org.citydb.ImpExp'

dependencies {
    compile project(':impexp-core')
    compile project(':impexp-plugin-api')
    compile project(':impexp-kml-collada-plugin')
    compile 'args4j:args4j:2.33'
    compile ('org.citydb:swingx-ws:1.1.0') {
        transitive = false
    }
    compile ('org.citydb:swingx-all:1.6.5-2') {
        transitive = false
    }
}

startScripts {
    doLast {
        def windowsScript = getWindowsScript()
        def unixScript = getUnixScript()
        windowsScript.text = windowsScript.text
                .replaceFirst('"%JAVA_EXE%" %DEFAULT_JVM_OPTS%.*', 'start /min "" $0')
                .replaceAll('3_D_CITY_DB_IMPORTER_EXPORTER_OPTS', 'IMPORTER_EXPORTER_OPTS')
        unixScript.text = unixScript.text
                .replaceAll('3_D_CITY_DB_IMPORTER_EXPORTER_OPTS', 'IMPORTER_EXPORTER_OPTS')
    }
}

distributions.main.contents {
    into('plugin-api') {
        from tasks.getByPath(':impexp-plugin-api:installDist')
    }
}