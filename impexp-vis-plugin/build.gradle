ext {
    genSrcDir = 'src-gen/main/java'
}

sourceSets {
    main.java.srcDir genSrcDir
}

configurations {
    jaxb
}

dependencies {
    api project(':impexp-core')
    api 'org.citydb:texture-atlas-creator:1.4.0'
    implementation 'java3d:j3d-core:1.5.2'
    implementation 'java3d:j3d-core-utils:1.5.2'
    implementation 'java3d:vecmath:1.5.2'

    jaxb 'org.glassfish.jaxb:jaxb-xjc:2.3.2'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:1.3'
    jaxb 'org.slf4j:slf4j-simple:1.7.25'
}

task generateJaxb(group: 'jaxb') {
    outputs.dir genSrcDir

    doLast {
        mkdir genSrcDir

        System.setProperty('javax.xml.accessExternalSchema', 'file')
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.xjc(destdir: genSrcDir,
                schema: 'resources/jaxb/kml-collada.xsd',
                binding: 'resources/jaxb/binding.xjb',
                extension: 'true',
                encoding: 'UTF-8') {
            produces(dir: genSrcDir, includes: '**/*.java')
            arg(line: '-Xnamespace-prefix -Xsetters -Xsetters-mode=direct')
        }

        delete "$genSrcDir/oasis"
    }
}

task cleanJaxb(type: Delete, group: 'jaxb') {
    delete 'src-gen'
}