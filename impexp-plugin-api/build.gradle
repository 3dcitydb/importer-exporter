import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'distribution'
apply plugin: 'maven-publish'

ext {
    pluginApiService = 'org.citydb.plugin.Plugin'
}

dependencies {
    compile project(':impexp-core')
}

javadoc {
    options.overview "$buildDir/tmp/javadoc/overview.html"
    options.addStringOption('doctitle', project.pluginApiName + ' - ' + project.version)

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    name: project.pluginApiName,
                    version: project.version,
                    impexpName: project.impexpName,
                    citydbWebsiteUrl: project.citydbWebsiteUrl,
                    vendorName: project.vendorName,
                    vendorOrganisation: project.vendorOrganisation,
                    vendorCountry: project.vendorCountry,
                    vendorCopyright: project.vendorCopyright,
                    vendorWebsiteUrl: project.vendorWebsiteUrl
            ])
        }
    }
}

distributions.main.contents {
    from('resources/doc/README.txt') {
        filter(ReplaceTokens, tokens: [
                name: project.pluginApiName,
                version: project.version,
                date: project.date.format('yyy-MM-dd'),
                pluginApiService: project.pluginApiService,
                pluginsDir: 'plugins',
                impexpName: project.impexpName,
                citydbName: project.citydbName,
                citydbWebsiteUrl: project.citydbWebsiteUrl,
                citydbVcsUrl: project.citydbVcsUrl,
                vendorName: project.vendorName,
                vendorOrganisation: project.vendorOrganisation,
                vendorCountry: project.vendorCountry,
                vendorCopyright: project.vendorCopyright,
                vendorWebsiteUrl: project.vendorWebsiteUrl
        ])
    }
    into('lib') {
        from jar
        from tasks.getByPath(':impexp-core:jar')
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from "$rootDir/resources/license/APACHE-2.0.txt"
        from "$rootDir/resources/license/LICENSE.txt"
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = '3dcitydb'
        name = project.name
        websiteUrl = project.citydbVcsUrl
        vcsUrl = project.impexpGit
        issueTrackerUrl = project.impexpIssueTrackerUrl
        labels = ['3dcitydb', 'importer-exporter', 'plugin-api']
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}