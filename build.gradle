import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.jfrog.artifactory' version '4.24.21'
}

apply from: 'properties.gradle'

version '5.2.1'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    group 'org.citydb'
    version parent.version

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        compileJava {
            options.release = 8
            options.encoding = 'UTF-8'
        }
        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        maven {
            url 'https://repo.osgeo.org/repository/release'
        }
        maven {
            url 'https://citydb.jfrog.io/artifactory/maven'
        }
        mavenCentral()
    }

    task processLicense(type: Copy) {
        from("$rootDir/resources/license/LICENSE.txt") {
            filteringCharset = 'UTF-8'
            filter(ReplaceTokens, tokens: [
                    name: project.impexpName
            ])
        }
        into "$buildDir/tmp/license"
    }

    javadoc {
        include 'org/citydb/**'
        options {
            overview "$buildDir/tmp/javadoc/overview.html"
            header "${project.name} ${project.version}"
            bottom """
                <a href="${impexpIssueTrackerUrl}">Report a bug or suggest an enhancement</a><br>
                ${project.name} is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a>.<br>
                Official website and more information at: <a target="_blank" href="${project.citydbWebsiteUrl}">${project.citydbWebsiteUrl}</a><br>
                Copyright &copy; ${project.vendorCopyright}<br>
                ${project.vendorName}<br>
                ${project.vendorOrganisation}, ${project.vendorCountry}<br>
                <a target="_blank" href="${project.vendorWebsiteUrl}">${project.vendorWebsiteUrl}</a>
                """.replaceAll("[\r|\n]+", "")
            addStringOption('doctitle', project.name + ' - ' + project.version)
            addStringOption('Xdoclint:none', '-quiet')
        }

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }

        doFirst {
            copy {
                from("$rootDir/resources/javadoc/overview.html") {
                    filteringCharset = 'UTF-8'
                    filter(ReplaceTokens, tokens: [
                            name: project.name,
                            version: project.version,
                            impexpName: project.impexpName,
                    ])
                }
                into "$buildDir/tmp/javadoc"
            }
        }
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.impexpName,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': project.vendorName + ', ' + project.vendorOrganisation
            )
        }
        into('META-INF') {
            from "$rootDir/resources/license/APACHE-2.0.txt"
            from processLicense
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = project.name
                    url = project.citydbVcsUrl
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = project.impexpVcsUrl
                        connection = 'scm:git:' + project.impexpGit
                    }
                }
            }
        }
    }

    artifactory {
        contextUrl = project.hasProperty('artifactory_contextUrl') ? project.property('artifactory_contextUrl') : System.getenv('artifactory_contextUrl')
        publish {
            repository {
                repoKey = 'maven'
                username = project.hasProperty('artifactory_user') ? project.property('artifactory_user') : System.getenv('artifactory_user')
                password = project.hasProperty('artifactory_password') ? project.property('artifactory_password') : System.getenv('artifactory_password')
                maven = true
            }
            defaults {
                publications('mavenJava')
            }
        }
    }
}