apply from: 'properties.gradle'

subprojects {
    group 'org.citydb'
    version '4.0'

    apply plugin: 'java'

    sourceCompatibility = 1.8

    repositories {
        maven {
            url 'http://download.osgeo.org/webdav/geotools/'
        }
        maven{
            url 'https://dl.bintray.com/3dcitydb/maven'
        }
        maven {
            url 'http://maven.imagej.net/content/repositories/thirdparty/'
        }
        jcenter()
    }

    plugins.withType(MavenPublishPlugin) {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        def createPom = {
            name project.name
            url project.citydbVcsUrl
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url project.impexpVcsUrl
                connection 'scm:git:' + project.impexpGit
            }
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                    pom.withXml {
                        asNode().children().last() + createPom
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5'
}